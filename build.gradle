/*
 * Copyright 2016 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'nebula.info' version '3.0.3'
    id 'nebula.contacts' version '3.0.1'

    id 'nebula.maven-publish' version '4.4.4'
    id 'nebula.maven-apache-license' version '4.4.4'
    id 'nebula.nebula-release' version '3.1.1'
    id 'nebula.nebula-bintray' version '3.1.0'
    id 'nebula.resolution-rules-producer' version '1.1.4'
}

description 'Rules for the Resolution Rules plugin'

group = 'com.netflix.nebula'

contacts {
    'dmthomas@gmail.com' {
        moniker 'Danny Thomas'
        github 'DanielThomas'
    }
}

subprojects {
    apply plugin: 'nebula.maven-publish'
    apply plugin: 'nebula.maven-apache-license'
    apply plugin: 'nebula.nebula-release'
    apply plugin: 'nebula.nebula-bintray'
    apply plugin: 'nebula.resolution-rules-producer'

    checkResolutionRulesSyntax {
        rules fileTree('src/rules').include('**/*.json')
    }

    tasks.jar.dependsOn 'checkResolutionRulesSyntax'

    publishing {
        publications {
            nebula(MavenPublication) {
                artifact packageRules
            }
        }
    }

    project.gradle.taskGraph.whenReady { TaskExecutionGraph graph ->
        project.tasks.bintrayUpload.onlyIf {
            graph.hasTask(':final') || graph.hasTask(':candidate')
        }
        project.tasks.artifactoryPublish.onlyIf {
            graph.hasTask(':snapshot') || graph.hasTask(':devSnapshot')
        }
    }
}
